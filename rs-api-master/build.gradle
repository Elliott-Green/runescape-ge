apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'java'
apply plugin: 'net.researchgate.release'

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath group: 'com.github.ben-manes', name: 'gradle-versions-plugin', version: '0.12.0'
        classpath group: 'net.researchgate', name: 'gradle-release', version: '2.3.5'
    }
}

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile group: 'com.google.code.gson', name: 'gson', version: '2.5'
    compile group: 'com.google.guava', name: 'guava', version: '19.0'
    compile group: 'org.apache.commons', name: 'commons-csv', version: '1.2'
    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.1'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.13'
    compile group: 'org.slf4j', name: 'slf4j-jdk14', version: '1.7.13'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

task aggregateJavadoc(type: Javadoc) {
    group 'Documentation'
    description 'Generates aggregated Javadoc API documentation for the main source code of all projects.'

    source allprojects.collect { project ->
        project.sourceSets.main.allJava
    }
    classpath = files(allprojects.collect { project ->
        project.sourceSets.main.compileClasspath
    })

    destinationDir file("${rootDir}/docs/javadoc/")

    options.showAll()

    configure(options) {
        windowTitle 'RuneScape API'
        docTitle 'RuneScape API'

        links 'http://docs.oracle.com/javase/8/docs/api/'
        links 'https://commons.apache.org/proper/commons-csv/apidocs/'
        links 'https://hc.apache.org/httpcomponents-client-ga/httpclient/apidocs/'
        links 'http://google-gson.googlecode.com/svn/trunk/gson/docs/javadocs/'
        links 'http://docs.guava-libraries.googlecode.com/git-history/release/javadoc/'
        links 'http://junit.org/javadoc/latest/'
        links 'http://www.slf4j.org/api/'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion '2.10'
    distributionUrl "https://services.gradle.org/distributions/gradle-${gradleVersion}-all.zip"
}
